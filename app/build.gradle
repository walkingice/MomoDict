apply plugin: 'com.android.application'
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: 'kotlin-kapt'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

check.dependsOn 'checkstyle'
check.dependsOn 'ktlint'

configurations {
    ktlint
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "org.zeroxlab.momodict"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    ext.androidXVersion = "1.0.0"
    ext.constraintLayoutLibVersion = "1.1.3"
    ext.kotlinCoroutinesVersion = '1.2.2'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.core:core:$androidXVersion"
    implementation "androidx.cardview:cardview:$androidXVersion"
    implementation "androidx.recyclerview:recyclerview:$androidXVersion"
    implementation "com.google.android.material:material:$androidXVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutLibVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${kotlinCoroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:${kotlinCoroutinesVersion}"

    ext.roomVersion = "2.1.0-alpha04"
    // Room, https://developer.android.com/topic/libraries/architecture/adding-components.html
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // For license detection
    // https://github.com/mikepenz/AboutLibraries
    implementation('com.mikepenz:aboutlibraries:5.9.4@aar') {
        transitive = true
    }

    // to install apache library
    implementation 'org.apache.commons:commons-compress:1.13'
    implementation group: 'commons-io', name: 'commons-io', version: '2.4'

    // use rx
    implementation 'io.reactivex:rxandroid:1.2.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    implementation 'io.reactivex:rxjava:1.1.6'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1"

    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.2-alpha01', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'

    testImplementation "org.robolectric:robolectric:3.5.1"
    testImplementation "org.mockito:mockito-core:2.12.0"

    // Test helpers for Room
    testImplementation 'androidx.room:room-testing:2.1.0-alpha04'

    ktlint 'com.github.shyiko:ktlint:0.9.2'
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // prepend "--reporter=plain?group_by_file" arg to change the reporter
    // see https://github.com/shyiko/ktlint
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/quality/square_picasso_checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

task findbugs(type: FindBugs) {
    source 'src'          // Specify the source code. The script should be applied to
    include '**/*.java'   // the module build.gradle so the 'src' folder resides at the same level
    exclude '**/gen/**'   // include / exclude folders and files.

    //Higher levels increase precision and find more bugs at the expense of running time and memory consumption.
    effort = 'max'
    //The priority threshold for reporting bugs. If set to {@code low}, all bugs are reported.
    reportLevel = 'low'

    excludeFilter = new File("${project.rootDir}/quality/findbugs_exclude.xml")

    classes = files("${project.rootDir}/app/build/intermediates/classes")
    classpath = files()
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

repositories {
    mavenCentral()
}
