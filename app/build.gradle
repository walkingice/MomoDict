apply plugin: 'com.android.application'
apply plugin: "kotlin-android"
apply plugin: "kotlin-parcelize"
apply plugin: 'kotlin-kapt'
apply plugin: 'checkstyle'

kotlin {
    jvmToolchain(21)
}

repositories {
    mavenCentral()
}

configurations {
    ktlint
}

android {
    namespace "cc.jchu.momodict"
    compileSdk 34
    compileOptions {
        sourceCompatibility = "21"
        targetCompatibility = "21"
    }

    defaultConfig {
        applicationId "cc.jchu.momodict"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
    }

    // ./gradlew --stacktrace :app:assembleDebug found issue:
    //     "More than one file was found with OS independent path 'META-INF/library-core_release.kotlin_module'"
    // maybe 'realm' should remove this file?
    packagingOptions {
        exclude 'META-INF/library-core_release.kotlin_module'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx
    implementation libs.androidx.fragment
    implementation libs.androidx.cardview
    implementation libs.androidx.recyclerview
    implementation libs.androidx.material
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.room.ktx
    kapt libs.androidx.room.compiler
    implementation libs.androidx.constraintlayout

    implementation libs.kotlinx.coroutines.android
    testImplementation libs.kotlinx.coroutines.test

    implementation libs.aboutlibraries
    implementation libs.commons.compress
    implementation libs.commons.io
    implementation libs.kotlin.stdlib

    testImplementation libs.junit
    testImplementation libs.robolectric
    testImplementation libs.mockito.core
    testImplementation libs.androidx.room.testing
    ktlint libs.ktlint

    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.2-alpha01', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // prepend "--reporter=plain?group_by_file" arg to change the reporter
    // see https://github.com/shyiko/ktlint
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/quality/square_picasso_checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}
